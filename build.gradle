plugins {
    id 'io.jmix' version '2.5.0'
    id 'java'
    id 'org.jetbrains.gradle.plugin.idea-ext' version '1.1.9'
}

apply plugin: 'org.springframework.boot'
apply plugin: 'com.vaadin'

jmix {
    bomVersion = '2.5.0'
}

group = 'io.jmix.uisamples'

repositories {
    mavenCentral()
    maven {
        url 'https://nexus.jmix.io/repository/public'
    }
    maven {
        url 'https://maven.vaadin.com/vaadin-addons'
    }
    maven {
        url = 'https://nexus.jmix.io/repository/premium'
        credentials {
            username = rootProject.hasProperty('premiumRepoUser')
                    ? rootProject['premiumRepoUser']
                    : System.getenv('CUBA_PREMIUM_USER')
            password = rootProject.hasProperty('premiumRepoPass')
                    ? rootProject['premiumRepoPass']
                    : System.getenv('CUBA_PREMIUM_PASSWORD')
        }
    }
}

vaadin {
    optimizeBundle = false
}

dependencies {
    implementation 'io.jmix.core:jmix-core-starter'
    implementation 'io.jmix.data:jmix-eclipselink-starter'
    implementation 'io.jmix.flowui:jmix-flowui-starter'
    implementation 'io.jmix.flowui:jmix-flowui-data-starter'
    implementation 'io.jmix.flowui:jmix-flowui-themes'
    implementation 'io.jmix.charts:jmix-charts-flowui-starter'
    implementation 'io.jmix.maps:jmix-maps-starter'
    implementation 'io.jmix.maps:jmix-maps-flowui-starter'
    implementation 'io.jmix.kanban:jmix-kanban-flowui-starter'
    implementation 'io.jmix.fullcalendar:jmix-fullcalendar-flowui-starter'
    implementation 'io.jmix.pivottable:jmix-pivottable-flowui-starter'

    implementation 'com.storedobject.chart:so-charts:3.2.4'
    implementation 'org.commonmark:commonmark:0.24.0'

    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.apache.commons:commons-dbcp2'

    runtimeOnly 'org.hsqldb:hsqldb'

    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }

    implementation 'io.jmix.translations:jmix-translations-ru'
}

configurations.implementation {
    exclude group: 'com.vaadin', module: 'hilla'
    exclude group: 'com.vaadin', module: 'hilla-dev'
    exclude group: 'com.vaadin', module: 'copilot'
}

idea {
    module {
        excludeDirs.addAll(files '.jmix', 'node_modules', 'src/main/frontend/generated/', 'src/main/bundles')
    }
}

processResources {
    with copySpec {
        from sourceSets.main.allJava
    }
}

test {
    useJUnitPlatform()
}